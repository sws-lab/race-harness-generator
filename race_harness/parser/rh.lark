%import common.WS
%ignore WS
%ignore COMMENT

COMMENT: /\/\/[^\n]*/
IDENTIFIER: /[a-zA-Z_]+[a-zA-Z0-9_]*/
INTEGER: /[0-9]+/

KW_RUN: "run"
KW_PROC: "proc"
KW_ENUM: "enum"
KW_NOOP: "noop"
KW_DO: "do"
KW_IN: "in"
KW_OUT: "out"
KW_SEND: "send"
KW_LOOP: "loop"
KW_BREAK: "break"
KW_CONTINUE: "continue"
KW_BRANCH: "branch"
KW_TRUE: "true"
KW_FALSE: "false"
KW_RECV: "recv"
KW_FROM: "from"
KW_THEN: "then"
KW_VAR: "var"
KW_SET: "set"
KW_HAS: "has"
KW_EMPTY: "empty"
KW_ADD: "add"
KW_DEL: "del"
KW_AND: "and"
KW_OR: "or"
KW_NONDET: "nondet"

LEFT_BRACKET: "["
RIGHT_BRACKET: "]"
LEFT_BRACE: "{"
RIGHT_BRACE: "}"
COLON: ":"

symbol: IDENTIFIER

var_type: KW_SET symbol -> set_var_type

condition: KW_TRUE -> cond_true
         | KW_FALSE -> cond_false
         | KW_RECV symbol (KW_FROM symbol)? -> cond_single_recv
         | KW_RECV LEFT_BRACE symbol RIGHT_BRACE (KW_FROM symbol)? -> cond_multi_recv
         | KW_SET symbol KW_HAS symbol -> cond_set_has
         | KW_SET symbol KW_EMPTY -> cond_set_empty
         | condition (KW_AND condition)+ -> cond_and
         | condition (KW_OR condition)+ -> cond_or
         | KW_NONDET -> cond_nondet

branch: condition KW_THEN stmt

stmt: KW_NOOP -> noop_stmt
    | LEFT_BRACE stmt* RIGHT_BRACE -> compound_stmt
    | KW_DO IDENTIFIER -> action_stmt
    | KW_SEND symbol symbol -> unicast_send_stmt
    | KW_SEND symbol LEFT_BRACE symbol+ RIGHT_BRACE -> multicast_send_stmt
    | KW_LOOP symbol stmt -> loop_stmt
    | KW_BREAK symbol -> break_stmt
    | KW_CONTINUE symbol -> continue_stmt
    | KW_BRANCH branch -> single_branch_stmt
    | KW_BRANCH LEFT_BRACE branch+ RIGHT_BRACE -> multi_branch_stmt
    | KW_VAR symbol COLON var_type -> var_decl_stmt
    | KW_SET symbol KW_ADD symbol -> set_add_stmt
    | KW_SET symbol KW_DEL symbol -> set_del_stmt

enum_entry: IDENTIFIER
enum_entries: enum_entry*
enum_decl: KW_ENUM IDENTIFIER LEFT_BRACE enum_entries RIGHT_BRACE

proc_protocol_decl: (KW_IN IDENTIFIER)? (KW_OUT IDENTIFIER)?
proc_decl: KW_PROC IDENTIFIER proc_protocol_decl stmt

proc_instance: KW_RUN IDENTIFIER IDENTIFIER -> proc_single_instance
             | KW_RUN IDENTIFIER LEFT_BRACKET INTEGER RIGHT_BRACKET IDENTIFIER -> proc_multi_instance

top_level_decl: proc_decl | proc_instance | enum_decl

module: top_level_decl*
